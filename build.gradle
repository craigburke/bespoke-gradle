buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.craigburke.gradle:client-dependencies:1.1.3'
        classpath 'com.bertramlabs.plugins:asset-pipeline-gradle:2.9.0'
        classpath 'org.ajoberstar:gradle-git:1.5.0'
    }
}

apply plugin: 'jetty'
apply plugin: 'com.craigburke.client-dependencies'
apply plugin: 'com.bertramlabs.asset-pipeline'
apply plugin: 'org.ajoberstar.github-pages'

ext {
    githubRepoUri = 'https://github.com/craigburke/bespoke-gradle.git'
}

clientDependencies {
    npm {
        'bespoke'('1.*')
        'bespoke-classes'('1.*', into: 'bespoke/plugins')
        'bespoke-scale'('1.*', into: 'bespoke/plugins')
        'bespoke-keys'('1.*', into: 'bespoke/plugins')
        'bespoke-progress'('1.*', into: 'bespoke/plugins')
        'bespoke-hash'('1.*', into: 'bespoke/plugins')
        'bespoke-backdrop'('1.*', into: 'bespoke/plugins')
        'bespoke-bullets'('1.*', into: 'bespoke/plugins')
        'bespoke-state'('1.*', into: 'bespoke/plugins')
        'bespoke-overview'('1.*', into: 'bespoke/plugins', transitive: false)
        'bespoke-theme-cube'('1.*', into: 'bespoke/themes')

        'asciidoctor-bespoke'('*', url: 'asciidoctor/asciidoctor-bespoke', from: 'templates/slim') {
            include '*'
        }

        'highlightjs'('8.7.0') {
            include 'highlight.pack.min.js'
            include 'styles/darkula.css'
        }

        'font-awesome'('4.6.3') {
            include 'css/font-awesome.min.css', 'fonts/**'
        }
    }
}

assets {
    configOptions = [
        asciidoctor: [
            header_footer: true,
            template_dirs: [
                project.file('src/assets/vendor/asciidoctor-bespoke').absolutePath,
                project.file('src/assets/templates').absolutePath,
            ],
            attributes: [ icons: 'font' ]
        ]
    ]
    excludes = ['**/asciidoctor-bespoke/*', '**/slides/*.adoc']
}

githubPages {
    repoUri = project.githubRepoUri

    credentials {
        username = project.hasProperty('githubToken') ? project.githubToken : ''
        password = ''
    }

    pages {
        from assetCompile.destinationDir
    }
}

jettyRun {
    httpPort = 9000
    contextPath = ''
    webAppSourceDirectory = file('build/assets/')
}

publishGhPages.dependsOn assetCompile
jettyRun.dependsOn assetCompile

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}