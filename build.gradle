buildscript {
    ext {
        assetPipelineVersion = '2.9.0'
    }
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'io.ratpack:ratpack-gradle:1.3.3'
        classpath 'com.craigburke.gradle:client-dependencies:1.1.3'
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:${assetPipelineVersion}"
        classpath 'org.ajoberstar:gradle-git:1.5.0'
    }
}

apply plugin: 'io.ratpack.ratpack-groovy'
apply plugin: 'com.craigburke.client-dependencies'
apply plugin: 'com.bertramlabs.asset-pipeline'
apply plugin: 'org.ajoberstar.github-pages'

ext {
    githubRepoUri = 'https://github.com/craigburke/bespoke-gradle.git'
    assetPath = file('src/assets').absolutePath
    asciidoctorConfig = [
        header_footer: true,
        template_dirs: [
            "${assetPath}/vendor/asciidoctor-bespoke".toString(),
            "${assetPath}/templates".toString()
        ],
        attributes: [ icons: 'font' ]
    ]
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile ratpack.dependency("guice")
    compile "com.bertramlabs.plugins:ratpack-asset-pipeline:${assetPipelineVersion}"
    assets 'com.craigburke:asciidoctor-asset-pipeline:1.0.0'
}

clientDependencies {
    installDir = "${assetPath}/vendor"

    npm {
        'bespoke'('1.*')
        'bespoke-classes'('1.*', into: 'bespoke/plugins')
        'bespoke-scale'('1.*', into: 'bespoke/plugins')
        'bespoke-keys'('1.*', into: 'bespoke/plugins')
        'bespoke-progress'('1.*', into: 'bespoke/plugins')
        'bespoke-hash'('1.*', into: 'bespoke/plugins')
        'bespoke-backdrop'('1.*', into: 'bespoke/plugins')
        'bespoke-bullets'('1.*', into: 'bespoke/plugins')
        'bespoke-state'('1.*', into: 'bespoke/plugins')
        'bespoke-overview'('1.*', into: 'bespoke/plugins', transitive: false)
        'bespoke-theme-cube'('1.*', into: 'bespoke/themes')
        'asciidoctor-bespoke'('*', url: 'asciidoctor/asciidoctor-bespoke', from: 'templates/slim') {
            include '*'
        }
        'normalize.css'('4.1.1')
        'font-awesome'('4.6.3') {
            include 'css/font-awesome.min.css'
            include 'fonts/**'
        }
        'highlightjs'('8.7.0') {
            include 'highlight.pack.min.js'
            include 'styles/darkula.css'
        }
    }
}

assets {
    assetsPath = assetPath
    configOptions = [asciidoctor: asciidoctorConfig]
    excludes = ['**/*.slim', '**/*.rb', '**/slides/*', '**/code/*']
}

githubPages {
    repoUri = project.githubRepoUri

    credentials {
        username = project.hasProperty('githubToken') ? project.githubToken : ''
        password = ''
    }

    pages {
        from assetCompile.destinationDir
    }
}

run {
    doFirst {
        Map ratpackAssetConfig = asciidoctorConfig.findAll { it.key != 'template_dirs' }
        asciidoctorConfig.template_dirs?.eachWithIndex { String path, int index ->
            ratpackAssetConfig["template_dirs[${index}]"] = path
        }

        Map ratpackConfig = [ratpack: [assets:
           [url: '/', sourcePath: assetPath, assets: [asciidoctor: ratpackAssetConfig]]
        ]]
        ConfigObject configObject = new ConfigObject()
        configObject.putAll(ratpackConfig)
        systemProperties configObject.toProperties()

    }
}

publishGhPages.dependsOn assetCompile

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}